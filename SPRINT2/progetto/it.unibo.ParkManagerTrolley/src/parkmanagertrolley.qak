System -msglog model

Request clientRequest : clientRequest(X) // X = in
Reply enter : enter(SLOTNUM) // SLOTNUM=0/1/2/3/4/5/6 

Request carenter : carenter(SLOTNUM) // SLOTNUM=1/2/3/4/5/6
Reply receipt : receipt(TOKENID)

Dispatch outTokenid : outTokenid(TOKENID)

Dispatch move : move(X,Y)
Dispatch moveok   : move( MOVESHORT )
Dispatch stop : stop(X) //X=start/stop

Dispatch trolleystatusupdate : trolleystatusupdate(S)
Dispatch basicmd : basicmd(CMD)
Event localtrolleyupdate : localtrolleyupdate(S)

Dispatch cmd       : cmd(MOVE)
Request step       : step( TIME )	
Reply   stepdone   : stepdone(V)
Reply   stepfail   : stepfail(DURATION, CAUSE)

Context ctxparkmanager ip [host="localhost" port=8070]
Context ctxclient ip [host="192.168.1.68" port=8069]
Context ctxbasicrobot ip [host="127.0.0.1" port=8020]    

ExternalQActor basicrobot context ctxbasicrobot

QActor parkmanagerservice context ctxparkmanager {
	[# 
		var counter=0 
		var x = 0
		var y = 0
	#]
	State s0 initial {
		discardMsg Off
		solve( consult("parking.pl"))
		solve( dynamic("freeSlot/1") )
		solve( init(X) )
		
		qrun coap.actorQakStateCoapObserver.activate( myself )
		println("Starting ParkManagerService.")
	}
	Goto accept
	
	State accept {
		println("ParkManagerServing accepting client requests...")	
	}
	Transition clientMsg whenRequest clientRequest and [# coap.actorQakStateCoapObserver.readTrolley()!="stopped" #] -> handleClientRequest
						 whenRequest carenter and [# coap.actorQakStateCoapObserver.readTrolley()!="stopped" #] -> handleCarEnter
						 whenMsg outTokenid and [# coap.actorQakStateCoapObserver.readOutdoor()=="free" && coap.actorQakStateCoapObserver.readTrolley()!="stopped" #] -> handleClientOut
						 whenMsg stop -> stoptrolley
	
	State handleClientRequest {
		println("ParkManagerService handling client request")
		onMsg( clientRequest : clientRequest(X)) {
			[# val requestType = "${payloadArg(0)}" #]
			if [# requestType == "in" #] {
				
				if [# coap.actorQakStateCoapObserver.readWeight()=="free" #] {
				
					solve( getFreeSlot( S ) )
					[# val SLOTNUM = getCurSol("S") #]
					updateResource [# "enter("+SLOTNUM+")" #]
					
					solve( occupySlot( $SLOTNUM ) ) // let's assume at the moment that the client is going to do carenter
				
					replyTo clientRequest with  enter :  enter( $SLOTNUM )	
				}
				else {
					replyTo clientRequest with  enter :  enter( 0 ) //at the moment the client is refused if the indoor area is occupied
				}
			}
		}
	}
	Goto accept
	
	State handleCarEnter {
		printCurrentMessage
		onMsg(carenter : carenter(SLOTNUM)) {
			[# val SLOTNUM = payloadArg(0).toInt() #]
			
			solve( indoor(X,Y) )
			[# x = getCurSol("X").toString().toInt() #]
			[# y = getCurSol("Y").toString().toInt() #]
			
			//forward transporttrolley -m move : move($x,$y)
			[# forward("move", "move($x,$y)" ,"trolleylogic" ) #]
			
			
			solve( getCoordinates($SLOTNUM,X,Y) )
			[# x = getCurSol("X").toString().toInt() #]
			[# y = getCurSol("Y").toString().toInt() #]

			//forward transporttrolley -m move : move($x,$y)
			//[# forward("move", "move($x,$y)" ,"transporttrolley" ) #]
			[# forward("move", "move($x,$y)" ,"trolleylogic" ) #]
					
			[# val TOKENID = "$SLOTNUM"+"$counter" #]
			[# counter++ #]
			
			
			updateResource [# "receipt("+TOKENID+")" #]
					
			replyTo carenter with  receipt :  receipt( $TOKENID )
		}
	}
	Transition t3 whenTime 1000 -> moveTrolleyHome
						 whenRequest clientRequest and [# coap.actorQakStateCoapObserver.readTrolley()!="stopped" #] -> handleClientRequest
						 whenRequest carenter and [# coap.actorQakStateCoapObserver.readTrolley()!="stopped" #] -> handleCarEnter
						 whenMsg outTokenid and [# coap.actorQakStateCoapObserver.readOutdoor()=="free"  && coap.actorQakStateCoapObserver.readTrolley()!="stopped" #] -> handleClientOut
						 whenMsg stop -> stoptrolley
	
	State handleClientOut {
		println("ParkManagerServing out.")
		if [# coap.actorQakStateCoapObserver.readOutdoor()=="free" #] {
			onMsg( outTokenid : outTokenid(X)) {
				[# val TOKENID = "${payloadArg(0)}" #]
				[# val SLOTNUM =  TOKENID.first() #]
					
				solve( getCoordinates($SLOTNUM,X,Y) )
				[# x = getCurSol("X").toString().toInt() #]
				[# y = getCurSol("Y").toString().toInt() #]
					
				//forward transporttrolley -m move : move($x,$y)
				[# forward("move", "move($x,$y)" ,"trolleylogic" ) #]
					
				solve( outdoor(X,Y) )
				[# x = getCurSol("X").toString().toInt() #]
				[# y = getCurSol("Y").toString().toInt() #]
					
				//forward transporttrolley -m move : move($x,$y)
				//[# forward("move", "move($x,$y)" ,"transporttrolley" ) #]
				[# forward("move", "move($x,$y)" ,"trolleylogic" ) #]
					
				solve( unoccupySlot( $SLOTNUM ) ) 
			}
		}
	}
	Transition t4 whenTime 1000 -> moveTrolleyHome
				  whenRequest clientRequest and [# coap.actorQakStateCoapObserver.readTrolley()!="stopped" #] -> handleClientRequest
				  whenRequest carenter and [# coap.actorQakStateCoapObserver.readTrolley()!="stopped" #] -> handleCarEnter
				  whenMsg outTokenid and [# coap.actorQakStateCoapObserver.readOutdoor()=="free" && coap.actorQakStateCoapObserver.readTrolley()!="stopped" #] -> handleClientOut
				  whenMsg stop -> stoptrolley
				  
	State moveTrolleyHome {
		solve( home(X,Y) )
		[# x = getCurSol("X").toString().toInt() #]
		[# y = getCurSol("Y").toString().toInt() #]
		//forward transporttrolley -m move : move($x,$y)
		//[# forward("move", "move($x,$y)" ,"transporttrolley" ) #]
		[# forward("move", "move($x,$y)" ,"trolleylogic" ) #]
	}
	Goto accept
	 
	State stoptrolley {
		onMsg( stop : stop(CMD)) {
			[# val CMD = "${payloadArg(0)}" #]
			//propaga il comando al trolley
			
			//forward trolleylogic -m stop : stop($CMD)
			forward transporttrolley -m stop : stop($CMD)
		}
	} Goto accept
}

QActor transporttrolley context ctxparkmanager {
	
	State s0 initial {
	} 
	Goto wait
	
	State wait {
	}
	Transition t0 whenMsg trolleystatusupdate -> updatestatus
				  whenMsg stop -> handleMsg
	
	State updatestatus {
		onMsg(trolleystatusupdate : trolleystatusupdate(S)) {
			[# 
				var STATE = payloadArg(0)
			#]
			updateResource [# STATE #]
		}
	}
	Goto wait
	
	State handleMsg {
		onMsg(stop : stop(CMD)) {
			
			[# 
				var CMD = payloadArg(0)
			#]
			
			emit localtrolleyupdate : localtrolleyupdate($CMD)
		}
	}
	Goto wait
}

QActor trolleylogic context ctxparkmanager {
	[#
		val mapname     = "parkingMap"  		 
		var Myself      = myself   
		var CurrentPlannedMove = "" 
		var x = ""
		var y = ""
		var DIST = ""
		var PREV = "176"
		var state = ""
	#]
	
	State s0 initial {
		run itunibo.planner.plannerUtil.initAI()
		run itunibo.planner.plannerUtil.loadRoomMap("$mapname") 
		run itunibo.planner.plannerUtil.showMap( )   
     	run itunibo.planner.plannerUtil.showCurrentRobotState()
	}
	Goto wait
	
	State wait {
		if [# itunibo.planner.plannerUtil.atHome() #] {
 			//updateResource [# "idle" #]
 			forward transporttrolley -m trolleystatusupdate : trolleystatusupdate(idle)
 		} else {
 			//updateResource [# "working" #]
 			forward transporttrolley -m trolleystatusupdate : trolleystatusupdate(working)
 		}
		println("Waiting messages...")
	}
	Transition t0 whenMsg move -> handle
				  whenEvent localtrolleyupdate -> stopped
	
	State handle {
		onMsg(move : move(X,Y)) {
			[# 
				x = payloadArg(0)
				y = payloadArg(1)
			#]
			println("x: $x, y: $y")
			
			//updateResource [# "working" #]
			forward transporttrolley -m trolleystatusupdate : trolleystatusupdate(working)
			
			run itunibo.planner.plannerUtil.planForGoal(x,y)
		}
	}
	Goto execPlannedMoves     
    
	State execPlannedMoves{   
		delay 300
 		[#  CurrentPlannedMove = itunibo.planner.plannerUtil.getNextPlannedMove() #]
 		println("+++++++++++++++++++++++++++++++ $CurrentPlannedMove")
 	}
 	Goto doMove if [# CurrentPlannedMove.length>0 #] else parkthecar
 
 	State doMove{ 		
 	}
 	Goto wMove if [# CurrentPlannedMove == "w" #] else turnMove
 	
 	State wMove {     
 		//request basicrobot -m step : step(175) //330
 		forward trolleyintermediary -m basicmd : basicmd($CurrentPlannedMove)
 	}
 	Goto stepDone 
  	
 	 State stepDone{  
 		run itunibo.planner.plannerUtil.updateMap(  "w" )
 		
 		//[# var MAP = itunibo.planner.plannerUtil.getMap() #]
 		//println("mappa: $MAP")
 		//forward transporttrolley -m trolleystatusupdate : trolleystatusupdate($MAP)
 		
 		run itunibo.planner.plannerUtil.showCurrentRobotState( )
  	} 
 	Goto execPlannedMoves
 	
 	State turnMove{
  		if [# CurrentPlannedMove == "l" || CurrentPlannedMove == "r"  #]{
  			//forward basicrobot -m cmd : cmd($CurrentPlannedMove)
  			forward trolleyintermediary -m  basicmd : basicmd($CurrentPlannedMove)
  		} 
 	} 
 	Goto rotationDone

 	State rotationDone{
 		run itunibo.planner.plannerUtil.updateMap(  "$CurrentPlannedMove" )
 	} 
 	 Goto execPlannedMoves
 	
 	State parkthecar{
 		printCurrentMessage
 		run itunibo.planner.plannerUtil.showCurrentRobotState( )  
 		if [# ! itunibo.planner.plannerUtil.atPos(x.toInt(),y.toInt()) #]{
 			run itunibo.planner.plannerUtil.planForGoal(x,y)
 		}
 		else {
 			[# println(itunibo.planner.plannerUtil.get_curPos().toString()) #]
 			if [# itunibo.planner.plannerUtil.atHome() #] {
 				//updateResource [# "idle" #]
 				forward transporttrolley -m trolleystatusupdate : trolleystatusupdate(idle)
 			}
 			//updateResource [# "pos" + itunibo.planner.plannerUtil.get_curPos().toString() #]
 		}
 	}
    Goto execPlannedMoves  if[# ! itunibo.planner.plannerUtil.atPos(x.toInt(),y.toInt()) #] else checkStop
 	
 	State checkStop {
 	} 
 	Transition t1 	whenTime 200 -> wait
 					whenEvent localtrolleyupdate -> stopped
 	
 	State stopped {
 		forward transporttrolley -m trolleystatusupdate : trolleystatusupdate(stopped)
 	}
 	Transition t1 whenEvent localtrolleyupdate -> checkStart
 	
 	State checkStart {
 		onMsg(localtrolleyupdate : localtrolleyupdate(S)) {
			[# 
				state = payloadArg(0)
			#]
		}
 	}
 	Goto wait if [# state=="start" #] else stopped
 	
}

QActor trolleyintermediary context ctxparkmanager {
	[#
		var CMD = ""
	#]
	
	State s0 initial {
	} 
	Goto wait
	
	State wait {
		
	}
	Transition t0 whenMsg basicmd -> handleCmd
	
	State handleCmd {
		onMsg(basicmd : basicmd(CMD)) {
			[# 
				CMD = payloadArg(0)
			#]
			if [# CMD == "l" || CMD == "r" #] {
				forward basicrobot -m cmd : cmd($CMD)
			}
		}
	}
	Goto stepMove if [# CMD == "w" #] else wait 
	
	State stepMove {
		request basicrobot -m step : step(175)
	}
	Transition t0 whenReply stepdone   -> wait
 				  whenReply stepfail   -> stepFailed
 				 
 				  
 	State stepFailed{  //has found an obstacle (a wall)
		printCurrentMessage
		onMsg(stepfail : stepfail(DURATION,OBSTACLE)) {
			[# 
				var DIST = payloadArg(0) //sostituire con pathexecutil.waitUser("Reposition the robot",10000)
			#]
			request basicrobot -m step : step( $DIST ) //da sostituire solo temporaneo per prove!!
		}
 	}    
    Transition t1 whenReply stepdone   -> wait
 				  whenReply stepfail   -> stepFailed 
	
}

QActor parkingstate context ctxparkmanager {
	[#
		var sonarobs = coap.actorQakResourceCoapObserver("sonarsensor","8071","ctxsonarsensor")
		var weightobs = coap.actorQakResourceCoapObserver("weightsensor","8072","ctxweightsensor")
		var thermometerobs = coap.actorQakResourceCoapObserver("thermometer","8073","ctxthermometer")
		var trolleyobs = coap.actorQakResourceCoapObserver("transporttrolley","8070","ctxparkmanager")
		var fanobs = coap.actorQakResourceCoapObserver("fan","8074","ctxfan")
	#]
	
	State s0 initial {
		println("Starting Parkingstate.")
		updateResource [# "Empty" #] //stato iniziale
		qrun sonarobs.activate( myself)
		qrun weightobs.activate( myself)
		qrun thermometerobs.activate( myself)
		qrun trolleyobs.activate( myself)
		qrun fanobs.activate(myself)
	}
	Goto wait
	
	State wait {
		updateResource [# "sonar:" + sonarobs.readContent() + "weight:" + weightobs.readContent() + "thermometer:" + thermometerobs.readContent() + "trolley:" + trolleyobs.readContent() + "fan:" + fanobs.readContent()#]
	}
	Transition t0 whenTime 300 -> wait
	
}